/*
 * $Id$
 *
 * Jaak environment model is an open-source multiagent library.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014 St√©phane GALLAND.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.jaak.environment.external

import io.sarl.core.Behaviors
import io.sarl.core.DefaultContextInteractions
import io.sarl.jaak.environment.external.influence.DropDownInfluence
import io.sarl.jaak.environment.external.influence.MotionInfluence
import io.sarl.jaak.environment.external.influence.PickUpInfluence
import io.sarl.jaak.environment.external.influence.SemanticChangeInfluence
import io.sarl.jaak.environment.external.perception.EnvironmentalObject
import io.sarl.jaak.environment.external.perception.Perceivable
import io.sarl.jaak.kernel.external.JaakPhysicSpace
import io.sarl.jaak.kernel.external.JaakPhysicSpaceConstants
import io.sarl.jaak.kernel.internal.JaakPhysicSpaceSpecification
import io.sarl.jaak.kernel.internal.SkillBinder
import java.io.Serializable
import org.arakhne.afc.math.continous.object2d.Vector2f
import org.arakhne.afc.math.discrete.object2d.Point2i

skill PhysicBodySkill implements PhysicBody {
	
	uses DefaultContextInteractions, Behaviors
	
	var physicSpace : JaakPhysicSpace
	
	var binder : SkillBinder
	
	def install() {
		var dc = defaultContext
		var spaceId = JaakPhysicSpaceConstants::getSpaceIDInContext(dc)
		physicSpace = dc.getOrCreateSpace(typeof(JaakPhysicSpaceSpecification), spaceId)
		if (physicSpace === null) {
			throw new IllegalStateException("No physic space found")
		}
		binder = new SkillBinder(asEventListener)
		physicSpace.spawnBody(binder)
	}
		
	def uninstall() {
		physicSpace?.killBody(binder)
		physicSpace = null
		binder = null
	}

	def move(direction : Vector2f, changeHeading : boolean) {
		physicSpace.influence(binder.currentTime, new MotionInfluence(binder.body, direction))
		if (changeHeading) {
			heading = direction
		}
	}
	
	def moveForward(cells : int) {
		var body = binder.body
		var view = body.headingVector.clone
		view.normalize
		view.scale(cells)
		physicSpace.influence(binder.currentTime, new MotionInfluence(body, view))
	}
	
	def moveBackward(cells : int) {
		var body = binder.body
		var view = body.headingVector.clone
		view.normalize
		view.scale(-cells)
		physicSpace.influence(binder.currentTime, new MotionInfluence(body, view))
	}

	def turnLeft(radians : float) {
		physicSpace.influence(binder.currentTime, new MotionInfluence(binder.body, -radians))
	}

	def turnRight(radians : float) {
		physicSpace.influence(binder.currentTime, new MotionInfluence(binder.body, radians))
	}

	def setHeading(radians : float) {
		var body = binder.body
		var v = radians - body.headingAngle;
		physicSpace.influence(binder.currentTime, new MotionInfluence(body, v))
	}
	
	def setHeading(direction : Vector2f) {
		heading = direction.orientationAngle
	}

	def dropOff(object : EnvironmentalObject) {
		physicSpace.influence(binder.currentTime, new DropDownInfluence(binder.body, object))
	}

	//TODO: def pickUp(type : Class<T>) : T with T extends Perceivable
	def pickUp(type : Class<? extends Perceivable>) : Perceivable {
		var body = binder.body
		for(obj : body.perceivedObjects) {
			if (type.isInstance(obj) && body.position == obj.position) {
				physicSpace.influence(binder.currentTime, new PickUpInfluence(body, obj));
				return type.cast(obj);
			}
		}
		return null
	}

	def pickUp(object : EnvironmentalObject) {
		physicSpace.influence(binder.currentTime, new PickUpInfluence(binder.body, object))
	}

	//TODO: def touchUp(type : Class<T>) : T with T extends EnvironmentalObject
	def touchUp(type : Class<? extends EnvironmentalObject>) : EnvironmentalObject {
		for(obj : binder.body.perceivedObjects) {
			if (type.isInstance(obj) && position == obj.position) {
				return type.cast(obj);
			}
		}
		return null;
	}

	def setSemantic(semantic : Serializable) {
		physicSpace.influence(binder.currentTime, new SemanticChangeInfluence(semantic))
	}

	def getPosition : Point2i {
		binder.body.position
	}

	def getHeadingAngle : float {
		binder.body.headingAngle
	}

	def getHeadingVector : Vector2f {
		binder.body.headingVector
	}
	
	def getSemantic : Serializable {
		binder.body.semantic
	}

	def getSpeed : float {
		binder.body.speed
	}
	
	def getX : int {
		position.x()
	}

	def getY : int {
		position.y()
	}
	
}