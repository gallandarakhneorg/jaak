package io.sarl.jaak.demos.traffic

import io.sarl.core.Behaviors
import io.sarl.core.Destroy
import io.sarl.core.Initialize
import io.sarl.core.Lifecycle
import io.sarl.jaak.demos.traffic.behaviors.DrivingCapacity
import io.sarl.jaak.demos.traffic.behaviors.EmergencyDriver
import io.sarl.jaak.demos.traffic.behaviors.GPSCapacity
import io.sarl.jaak.demos.traffic.behaviors.GPSSkill
import io.sarl.jaak.demos.traffic.behaviors.RoadhogDrivingSkill
import io.sarl.jaak.demos.traffic.behaviors.StandardDriver
import io.sarl.jaak.demos.traffic.behaviors.StandardDrivingSkill
import io.sarl.jaak.demos.traffic.environment.MapUtil
import io.sarl.jaak.demos.traffic.logging.SimulationAgentLoggerCapacity
import io.sarl.jaak.demos.traffic.logging.SimulationAgentLoggerSkill
import io.sarl.jaak.demos.traffic.logging.SimulationEmergencyAgentLoggerCapacity
import io.sarl.jaak.demos.traffic.logging.SimulationEmergencyAgentLoggerSkill
import io.sarl.jaak.environment.BodyCreated
import io.sarl.jaak.environment.Perception
import io.sarl.jaak.environment.PhysicBody
import io.sarl.jaak.environment.PhysicBodySkill
import io.sarl.jaak.environment.SimulationStopped
import io.sarl.lang.core.Behavior
import org.arakhne.afc.math.continous.object2d.Vector2f
import org.arakhne.afc.math.discrete.object2d.Point2i

/** This class defines a driver.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
agent Driver {

	uses Behaviors, Lifecycle

	var tmpBehavior : PerceptionCatcher

	on Initialize {
		synchronized(this) {
			var body = new PhysicBodySkill
			// Select the driving capacity:
			//    ROADHOG
			//setSkill(typeof(DrivingCapacity), new RoadhogDrivingSkill) 
			//    STANDARD
			setSkill(typeof(DrivingCapacity), new StandardDrivingSkill)
			
			setSkill(typeof(GPSCapacity), new GPSSkill)
			setSkill(typeof(PhysicBody), body)
			tmpBehavior = new PerceptionCatcher(this)
			registerBehavior(tmpBehavior)
		}
	}
	
	on Destroy {
		<SimulationAgentLoggerSkill>clearSkill(typeof(SimulationAgentLoggerCapacity));
		<SimulationEmergencyAgentLoggerSkill>clearSkill(typeof(SimulationEmergencyAgentLoggerCapacity));
		<PhysicBodySkill>clearSkill(typeof(PhysicBody));
		<GPSSkill>clearSkill(typeof(GPSCapacity));
		<StandardDrivingSkill>clearSkill(typeof(DrivingCapacity));
	}
	
	def createEmergencyDriverBehavior : Behavior {
		var crashes = MapUtil::crashPositions
		var target = crashes.get((Math.random * crashes.size) as int)
		new EmergencyDriver(this, target)
	}
	
	def createStandardDriverBehavior(target : Point2i) : Behavior {
		new StandardDriver(this, target)
	}
	
	on BodyCreated {
		synchronized(this) {
			var beh : Behavior
			if (occurrence.body.semantic == EmergencyDriver) {
				setSkill(typeof(SimulationEmergencyAgentLoggerCapacity), new SimulationEmergencyAgentLoggerSkill) 
				beh = createEmergencyDriverBehavior
			} else {
				setSkill(typeof(SimulationAgentLoggerCapacity), new SimulationAgentLoggerSkill) 
				var v = new Vector2f(occurrence.worldWidth / 2, occurrence.worldHeight / 2)
				v.sub(occurrence.body.position.x(), occurrence.body.position.y());
				v.normalize
				var angle = v.orientationAngle + ((Math.random - Math.random) * Math.PI) as float
				v = Vector2f::toOrientationVector(angle)
				v.scale(10 * Math.max(occurrence.worldWidth, occurrence.worldHeight))
				var target = new Point2i(occurrence.body.position)
				target.add(v)
				target = MapUtil::normalizePoint(target)
				beh = createStandardDriverBehavior(target)
			}
			var b = tmpBehavior
			tmpBehavior = null
			if (b !== null) {
				unregisterBehavior(b)
			}
			registerBehavior(beh)
			if (b !== null) {
				for (evt : b.perceptions) {
					wake(evt)
				}
			}
		}
	}
	
	on SimulationStopped {
		synchronized(this) {
			killMe
		}
	}
	
}

behavior PerceptionCatcher {
	
	val perception = <Perception>newLinkedList

	on Perception {
		perception += occurrence
	}
	
	def getPerceptions : Iterable<Perception> {
		return perception
	} 
	
}