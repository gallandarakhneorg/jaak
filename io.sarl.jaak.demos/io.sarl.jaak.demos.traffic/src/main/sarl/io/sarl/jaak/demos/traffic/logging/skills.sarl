package io.sarl.jaak.demos.traffic.logging

import io.sarl.jaak.demos.traffic.environment.Path
import java.io.File
import java.io.FileWriter
import java.io.PrintWriter
import org.arakhne.afc.math.discrete.object2d.Point2i

/** This skill defines the capacity to log simulation information from an agent.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
skill SimulationAgentLoggerSkill implements SimulationAgentLoggerCapacity {

	var logFile : File
	
	var t : float = 0
	var path : Path
	var emergencySirenPosition : Point2i
	var position : Point2i
	var isStopDecidedWhenEmergency : boolean

	def install {
		var logFolder = SimulationLogger::logger.loggingFolder
		var agentFolder = new File(logFolder, "agents")
		agentFolder.mkdirs
		this.logFile = new File(agentFolder, "a_" + owner.ID + ".csv")
		var writer = new PrintWriter(this.logFile)
		writer.println("t\tPosition\tSiren?\tSiren Position\tStop?\tPath")
		writer.close
	}
	
	def logPath(position : Point2i, path : Path, t : float) {
		if (t >= this.t) {
			this.t = t
			this.path = path
			this.position = position.clone
		}
	}
	
	def logEmergencyVehicleStarted(newPath : Path, t : float) {
		if (t >= this.t) {
			this.path = path;
		}
	}

	def logEmergencySirenSignal(position : Point2i, isStopDecided : boolean, t : float) {
		if (t >= this.t) {
			this.emergencySirenPosition = position
			this.isStopDecidedWhenEmergency = isStopDecided
		}
	}

	def synchronizeLogs {
		var fwriter = new FileWriter(this.logFile, true)
		var writer = new PrintWriter(fwriter)
		writer.print(this.t)
		writer.print("\t")
		writer.print(if (this.position === null) "" else position.x() + " " + position.y())
		writer.print("\t")
		writer.print(if (this.emergencySirenPosition !== null) 1 else 0)
		writer.print("\t")
		if (this.emergencySirenPosition !== null) {
			writer.print(this.emergencySirenPosition.x())
			writer.print(" ")
			writer.print(this.emergencySirenPosition.y())
		}
		writer.print("\t")
		writer.print(if (this.isStopDecidedWhenEmergency) 1 else 0)
		writer.print("\t")
		if (this.path !== null) {
			var isFirst = true
			for (p : path) {
				if (isFirst) {
					isFirst = false
				} else {
					writer.print(";")
				}
				writer.print(p.x())
				writer.print(" ")
				writer.print(p.y())
			}
		}
		writer.print("\n")
		writer.flush
		writer.close
	}
	
}

/** This skill defines the capacity to log simulation information from an agent.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 */
skill SimulationEmergencyAgentLoggerSkill implements SimulationEmergencyAgentLoggerCapacity {

	var logFile : File
	
	var startTime : float = -1
	var endTime : float = -1

	def install {
		var logFolder = SimulationLogger::logger.loggingFolder
		this.logFile = new File(logFolder, "emergencyVehicle_" + owner.ID + ".csv")
		var writer = new PrintWriter(this.logFile)
		writer.println("Start time\tEnd time")
		writer.close
	}
	
	def logDrivingDuringEmergencySituation(t : float) {
		if (startTime < 0) {
			startTime = t
		}
		endTime = t
	}
	
	def synchronizeLogs {
		var writer = new PrintWriter(this.logFile)
		writer.println("Start time\tEnd time")
		writer.print(this.startTime)
		writer.print("\t")
		writer.print(this.endTime)
		writer.print("\n")
		writer.flush
		writer.close
	}
		
}
